plugins {
    id 'io.spring.dependency-management' version '1.1.5'
    id 'org.springframework.boot' version '3.3.4'
    id 'org.sonarqube' version '5.0.0.4638'
    id "org.owasp.dependencycheck" version "10.0.4"
}

ext {
    logbackEcsEncoderVersion = '1.6.0'
    okhttpVersion = '4.12.0'
    shedlockVersion = '5.16.0'
    springDocVersion = '1.8.0'
    zip4jVersion = '2.11.5'

    springbootMavenBomVersion = '3.3.4'

    nvdMirror = 'https://vulnz-nvd-mirror-devtest.intyg.nordicmedtest.se'
}

group = 'se.inera.intyg.cts'
version = System.properties['buildVersion'] ?: '0.0.1-SNAPSHOT'

allprojects {
    repositories {
        mavenCentral()
    }
}

sonar {
    properties {
        property "sonar.projectKey", "intyg-cts"
        property "sonar.projectName", "intyg-cts"
        property "sonar.dependencyCheck.htmlReportPath", "${buildDir}/reports/dependency-check-report.html"
        property "sonar.dependencyCheck.jsonReportPath", "${buildDir}/reports/dependency-check-report.json"
    }
}

dependencyCheck {
    formats = List.of("HTML", "JSON")

    nvd {
        datafeedUrl = "${nvdMirror}"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'org.sonarqube'
    apply plugin: 'org.owasp.dependencycheck'

    sourceCompatibility = '21'
    targetCompatibility = '21'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}"
        }
    }

    jacocoTestReport {
        dependsOn test

        reports {
            xml.required = true
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        "**/testability/**"
                ])
            }))
        }
    }

    sonar {
        properties {
            property "sonar.exclusions", "**/testability/**"
        }
    }
}
