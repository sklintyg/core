plugins {
    alias(libs.plugins.org.springframework.boot)
    alias(libs.plugins.org.sonarqube)
    alias(libs.plugins.org.owasp.dependencycheck)
    alias(libs.plugins.com.github.ben.manes.versions)
    id 'test-report-aggregation'
    id 'application'
}

ext {
    intygBomVersion = System.properties['intygBomVersion'] ?: '0.0.1-SNAPSHOT'
    ineraMavenRepository = 'https://nexus.drift.inera.se/repository/it-public/'
    nvdMirror = 'https://vulnz-nvd-mirror-devtest.intyg.nordicmedtest.se'
}

application {
    mainClass = 'se.inera.intyg.certificateprintservice.CertificatePrintServiceApplication'
}

group = 'se.inera.intyg.certificateprintservice'
version = System.properties['buildVersion'] ?: '0.0.1-SNAPSHOT'

apply from: 'gradle/configureIntygBom.gradle'

allprojects {
    repositories {
        mavenLocal()
        maven { url = ineraMavenRepository }
        mavenCentral()
    }
}

dependencyCheck {
    formats = List.of("HTML", "JSON")
    nvd.setDatafeedUrl(nvdMirror)
    analyzers.setAssemblyEnabled(false)
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    java {
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        implementation platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        annotationProcessor platform("se.inera.intyg.bom:platform:${intygBomVersion}")
    }

    jacocoTestReport {
        dependsOn test

        reports {
            xml.required = true
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        "**/testability/**",
                        "**/integrationtest/**"
                ])
            }))
        }
    }

    sonar {
        properties {
            property "sonar.projectKey", "intyg-certificate-print-service"
            property "sonar.projectName", "intyg-certificate-print-service"
            property "sonar.dependencyCheck.jsonReportPath", "${layout.buildDirectory}/reports/dependency-check-report.json"
            property "sonar.dependencyCheck.htmlReportPath", "${layout.buildDirectory}/reports/dependency-check-report.html"
            property "sonar.exclusions", "**/testability/**,**/integrationtest/**"
        }
    }

    tasks.withType(Test).configureEach {
        jvmArgs '-Xshare:off'
    }
}

reporting {
    reports {
        integrationTestAggregateTestReport(AggregateTestReport) {
            testSuiteName = 'integrationTest'
        }
    }

    dependencies {
        testReportAggregation project(':app')
        testReportAggregation project(':playwright-pdf-generator')
        testReportAggregation project(':pdf-generator-api')
    }
}
