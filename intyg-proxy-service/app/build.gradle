plugins {
    id 'org.springframework.boot' version '3.3.4'
}

def applicationDir = "${rootProject.projectDir}/devops/dev"

dependencies {
    implementation project(':integration-api')
    implementation project(':logging')
    runtimeOnly project(':integration-common')
    runtimeOnly project(':integration-pu')
    runtimeOnly project(':integration-pu-v4')
    runtimeOnly project(':integration-fakepu')
    runtimeOnly project(':integration-hsa')
    runtimeOnly project(':integration-fakehsa')

    implementation "co.elastic.logging:logback-ecs-encoder:${logbackEcsEncoderVersion}"
    implementation "com.google.guava:guava-gwt:${guavaVersion}"
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    compileOnly 'org.projectlombok:lombok'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform {
    }
}

bootRun {
    systemProperty("application.dir", "${rootProject.projectDir}/devops/dev")
    systemProperty("spring.profiles.active", "testability,dev,fakepu,fakehsa")
    systemProperty("spring.config.additional-location", "file:${applicationDir}/config/")
}

tasks.register("appRunDebug") {
    println("######## Running in Debug mode ########")
    doFirst {
        bootRun.configure {
            jvmArgs = [
                    "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=18021",
                    "-Dlogging.config=${rootProject.projectDir}/devops/dev/config/logback-spring.xml"
            ]
        }
    }
    finalizedBy("bootRun")
}

tasks.register("appRun") {
    println("######## Running in normal mode ########")
    doFirst {
        bootRun.configure {
            jvmArgs = ["-Dlogging.config=${rootProject.projectDir}/devops/dev/config/logback-spring.xml"]
        }
    }
    finalizedBy("bootRun")
}
