plugins {
    id "com.yupzip.wsdl2java" version "3.0.0" apply true
}

dependencies {
    implementation project(':integration-api')
    implementation project(':integration-common')
    implementation project(':logging')
    implementation "org.springframework:spring-context"
    implementation "org.springframework.boot:spring-boot-starter-web-services"
    implementation "org.apache.cxf:cxf-rt-frontend-jaxws"
    implementation "org.apache.cxf:cxf-rt-transports-http"

    compileOnly "org.projectlombok:lombok"
    compileOnly "org.slf4j:slf4j-api"
    annotationProcessor "org.projectlombok:lombok"

    wsdl2java platform("se.inera.intyg.bom:platform:${bomVersion}")
    wsdl2java "codes.rafael.jaxb2_commons:jaxb2-basics"
    wsdl2java "codes.rafael.jaxb2_commons:jaxb2-basics-runtime"
    wsdl2java "com.sun.xml.bind:jaxb-impl"
    wsdl2java "com.sun.xml.ws:rt"
    wsdl2java "jakarta.xml.ws:jakarta.xml.ws-api"
    wsdl2java "org.apache.cxf.xjc-utils:cxf-xjc-runtime"
    wsdl2java "org.jvnet.jaxb2_commons:jaxb2-namespace-prefix"

    testImplementation "org.springframework:spring-context"
    testImplementation "org.springframework:spring-test"
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
    testImplementation "org.mockito:mockito-junit-jupiter"
    testRuntimeOnly "org.slf4j:slf4j-simple"
}

tasks.named('test') {
    useJUnitPlatform()
}

wsdl2java {
    wsdlDir = file("src/main/resources/schemas")
    cxfVersion = project.getProperties().get("cxfVersion")
    cxfPluginVersion = project.getProperties().get("cxfPluginVersion")
    includeJava8XmlDependencies = false

    def personVersion = "3.1"

    def commonOptions = [
            "-p",
            "urn:riv:itintegration:registry:1=se.riv.itintegration.registry.v1",
    ]

    def commonPersonOptions = commonOptions + [
            "-p",
            "urn:riv:strategicresourcemanagement:persons:person:3=se.riv.strategicresourcemanagement.persons.person.v3"
    ]

    wsdlsToGenerate = [
            commonPersonOptions + [
                    "-p",
                    "urn:riv:strategicresourcemanagement:persons:person:GetPersonsForProfileResponder:3=se.riv.strategicresourcemanagement.persons.person.getpersonsforprofileresponder.v3",
                    "-p",
                    "urn:riv:strategicresourcemanagement:persons:person:GetPersonsForProfileInitiator:3=se.riv.strategicresourcemanagement.persons.person.getpersonsforprofileinitiator.v3",
                    "-p",
                    "urn:riv:strategicresourcemanagement:persons:person:GetPersonsForProfile:3:rivtabp21=se.riv.strategicresourcemanagement.persons.person.getpersonsforprofile.v3.rivtabp21",
                    "-wsdlLocation",
                    "classpath:interactions/GetPersonsForProfileInteraction/GetPersonsForProfileInteraction_3.1_RIVTABP21.wsdl",
                    "$wsdlDir/person/$personVersion/interactions/GetPersonsForProfileInteraction/GetPersonsForProfileInteraction_3.1_RIVTABP21.wsdl"]
    ]
}