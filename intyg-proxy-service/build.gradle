plugins {
    id 'org.springframework.boot' version '3.1.2'
    id 'io.spring.dependency-management' version '1.1.2'
    id 'org.sonarqube' version '4.4.1.3373'
    id "org.owasp.dependencycheck" version "7.4.4"
}

ext {
    cxfVersion = "4.0.0"
    cxfPluginVersion = "4.0.0"
    jaxb2Version = "3.0.0"
    jaxb2NamespacePrefixVersion = "2.0"
    jaxbImplVersion = "4.0.1"
    jaxsWsVersion = "4.0.1"
    jakartaWsVersion = "4.0.0"
    springbootMavenBomVersion = '3.1.2'
    guavaVersion = '32.1.2-jre'
}

group = 'se.inera.intyg.intyg-proxy-service'
version = '0.0.1-SNAPSHOT'

allprojects {
    repositories {
        mavenLocal()
        maven { url "https://nexus.drift.inera.se/repository/it-public/" }
        mavenCentral()
    }
}

sonar {
    properties {
        property "sonar.projectKey", "intyg-intyg-proxy-service"
        property "sonar.projectName", "intyg-intyg-proxy-service"
    }
}

dependencyCheck {
    formats = List.of("HTML", "JSON")
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'org.sonarqube'
    apply plugin: 'org.owasp.dependencycheck'

    sourceCompatibility = '17'
    targetCompatibility = '17'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}"
        }
    }

    jacocoTestReport {
        dependsOn test

        reports {
            xml.required = true
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }
    }

    sonar {
        properties {
            property "sonar.dependencyCheck.htmlReportPath", "${buildDir}/reports/dependency-check-report.html"
            property "sonar.dependencyCheck.jsonReportPath", "${buildDir}/reports/dependency-check-report.json"
        }
    }
}