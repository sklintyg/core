plugins {
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.sonarqube' version '4.4.1.3373'
    id "org.owasp.dependencycheck" version "9.0.9"
    id 'test-report-aggregation'
    id 'application'
}

ext {
    cxfVersion = "4.0.0"
    cxfPluginVersion = "4.0.0"
    jaxb2Version = "3.0.0"
    jaxb2NamespacePrefixVersion = "2.0"
    jaxbImplVersion = "4.0.4"
    jaxsWsVersion = "4.0.2"
    jakartaWsVersion = "4.0.0"
    lombokMapstructBindingVersion = "0.2.0"
    mapstructVersion = "1.5.5.Final"
    springbootMavenBomVersion = '3.2.2'
    guavaVersion = '33.0.0-jre'

    ineraMavenRepository = 'https://nexus.drift.inera.se/repository/it-public/'
    nvdMirror = 'https://vulnz-nvd-mirror-devtest.intyg.nordicmedtest.se'
}

group = 'se.inera.intyg.intyg-proxy-service'
version = System.properties['buildVersion'] ?: '0.0.1-SNAPSHOT'
mainClassName = 'se.inera.intyg.intygproxyservice.IntygProxyServiceApplication'

allprojects {
    repositories {
        mavenLocal()
        maven { url "${ineraMavenRepository}" }
        mavenCentral()
    }
}

sonar {
    properties {
        property "sonar.projectKey", "intyg-intyg-proxy-service"
        property "sonar.projectName", "intyg-intyg-proxy-service"
        property "sonar.dependencyCheck.htmlReportPath", "${buildDir}/reports/dependency-check-report.html"
        property "sonar.dependencyCheck.jsonReportPath", "${buildDir}/reports/dependency-check-report.json"
    }
}

dependencyCheck {
    formats = List.of("HTML", "JSON")

    nvd {
        datafeedUrl="${nvdMirror}"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'org.sonarqube'
    apply plugin: 'org.owasp.dependencycheck'

    sourceCompatibility = '17'
    targetCompatibility = '17'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}"
        }
    }

    jacocoTestReport {
        dependsOn test

        reports {
            xml.required = true
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        "**/testability/**",
                        "**/integrationtest/**"
                ])
            }))
        }
    }
}

reporting {
    reports {
        integrationTestAggregateTestReport(AggregateTestReport) {
            testType = TestSuiteType.INTEGRATION_TEST
        }
    }

    dependencies {
        testReportAggregation project(':app')
        testReportAggregation project(':integration-api')
        testReportAggregation project(':integration-common')
        testReportAggregation project(':integration-fakehsa')
        testReportAggregation project(':integration-fakepu')
        testReportAggregation project(':integration-hsa')
        testReportAggregation project(':integration-pu')
        testReportAggregation project(':integration-pu-v4')
        testReportAggregation project(':integration-test')
    }
}