apply plugin: "java-test-fixtures"

dependencies {
    implementation project(':app')
    implementation project(':integration-api')
    implementation project(':integration-pu')

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-test'
    testImplementation 'io.github.microcks:microcks-testcontainers:0.2.3'
}

test {
    enabled = false
}

testing {
    suites {
        integrationTest(JvmTestSuite) {
            useJUnitJupiter()

            sources {
                java {
                    srcDirs = ['src/test/java']
                }
                resources {
                    srcDirs = ['src/test/resources']
                }
            }
            configurations {
                integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor
                integrationTestImplementation.extendsFrom testImplementation
                integrationTestCompileOnly.extendsFrom testCompileOnly
            }
            targets.all {
                testTask.configure {
                    shouldRunAfter(test)
                }
            }
        }
    }
}