plugins {
    id "com.yupzip.wsdl2java" version "3.0.0" apply true
}

dependencies {
    implementation project(':integration-api')
    implementation project(':integration-common')
    implementation "org.springframework:spring-context"
    implementation "org.springframework.boot:spring-boot-starter-web-services"
    implementation "org.apache.cxf:cxf-rt-frontend-jaxws:${cxfVersion}"
    implementation "org.apache.cxf:cxf-rt-transports-http:${cxfVersion}"

    compileOnly "org.projectlombok:lombok"
    compileOnly "org.slf4j:slf4j-api"
    annotationProcessor "org.projectlombok:lombok"

    wsdl2java "com.sun.xml.bind:jaxb-impl:${jaxbImplVersion}"
    wsdl2java "jakarta.xml.ws:jakarta.xml.ws-api:${jakartaWsVersion}"
    wsdl2java "com.sun.xml.ws:rt:${jaxsWsVersion}"
    wsdl2java "org.jvnet.jaxb2_commons:jaxb2-namespace-prefix:${jaxb2NamespacePrefixVersion}"
    wsdl2java "codes.rafael.jaxb2_commons:jaxb2-basics-runtime:${jaxb2Version}"
    wsdl2java "codes.rafael.jaxb2_commons:jaxb2-basics:${jaxb2Version}"

    testImplementation "org.springframework:spring-context"
    testImplementation "org.springframework:spring-test"
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
    testImplementation "org.mockito:mockito-junit-jupiter"
    testRuntimeOnly "org.slf4j:slf4j-simple"
}

tasks.named('test') {
    useJUnitPlatform()
}

wsdl2java {
    wsdlDir = file("src/main/resources/schemas")
    cxfVersion = project.getProperties().get("cxfVersion")
    cxfPluginVersion = project.getProperties().get("cxfPluginVersion")

    def personVersion5 = "5.0"

    def commonOptions = [
            "-p",
            "urn:riv:itintegration:registry:1=se.riv.itintegration.registry.v1",
    ]

    def commonPersonOptions = commonOptions + [
            "-p",
            "urn:riv:strategicresourcemanagement:persons:person:5=se.riv.strategicresourcemanagement.persons.person.v5"
    ]

    wsdlsToGenerate = [
            commonPersonOptions + [
                    "-p",
                    "urn:riv:strategicresourcemanagement:persons:person:GetPersonsForProfileResponder:5=se.riv.strategicresourcemanagement.persons.person.getpersonsforprofileresponder.v5",
                    "-p",
                    "urn:riv:strategicresourcemanagement:persons:person:GetPersonsForProfileInitiator:5=se.riv.strategicresourcemanagement.persons.person.getpersonsforprofileinitiator.v5",
                    "-p",
                    "urn:riv:strategicresourcemanagement:persons:person:GetPersonsForProfile:5:rivtabp21=se.riv.strategicresourcemanagement.persons.person.getpersonsforprofile.v5.rivtabp21",
                    "-wsdlLocation",
                    "classpath:interactions/GetPersonsForProfileInteraction/GetPersonsForProfileInteraction_5.0_RIVTABP21.wsdl",
                    "$wsdlDir/person/$personVersion5/interactions/GetPersonsForProfileInteraction/GetPersonsForProfileInteraction_5.0_RIVTABP21.wsdl"]
    ]
}