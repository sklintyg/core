plugins {
    id "com.yupzip.wsdl2java" version "3.0.0" apply true
}


dependencies {
    implementation project(':integration-api')
    implementation project(':integration-common')
    implementation project(':logging')
    implementation "org.springframework:spring-context"
    implementation "org.springframework.boot:spring-boot-starter-web-services"
    implementation "org.apache.cxf:cxf-rt-frontend-jaxws:${cxfVersion}"
    implementation "org.apache.cxf:cxf-rt-transports-http:${cxfVersion}"

    compileOnly "org.projectlombok:lombok"
    compileOnly "org.slf4j:slf4j-api"
    annotationProcessor "org.projectlombok:lombok"

    wsdl2java "codes.rafael.jaxb2_commons:jaxb2-basics:${jaxb2Version}"
    wsdl2java "codes.rafael.jaxb2_commons:jaxb2-basics-runtime:${jaxb2Version}"
    wsdl2java "com.sun.xml.bind:jaxb-impl"
    wsdl2java "com.sun.xml.ws:rt:${xmlWsRtVersion}"
    wsdl2java "jakarta.xml.ws:jakarta.xml.ws-api"
    wsdl2java "org.apache.cxf.xjc-utils:cxf-xjc-runtime:${cxfPluginVersion}"
    wsdl2java "org.jvnet.jaxb2_commons:jaxb2-namespace-prefix:${jaxb2NamespacePrefixVersion}"

    testImplementation "org.springframework:spring-context"
    testImplementation "org.springframework:spring-test"
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
    testImplementation "org.mockito:mockito-junit-jupiter"
    testRuntimeOnly "org.slf4j:slf4j-simple"
}

tasks.named('test') {
    useJUnitPlatform()
}

wsdl2java {
    wsdlDir = file("src/main/resources/schemas")
    cxfVersion = project.getProperties().get("cxfVersion")
    cxfPluginVersion = project.getProperties().get("cxfPluginVersion")
    includeJava8XmlDependencies = false

    def commonOptions = [
            "-p",
            "urn:mvk:itintegration:userprofile:2=se.mkv.itintegration.userprofile.v2",
            "-p",
            "urn:mvk:itintegration:userprofile:GetUserProfileResponder:2=se.mkv.itintegration.getuserprofileresponder.v2",
            "-p",
            "urn:mvk:itintegration:userprofile:GetUserProfile:2:rivtabp21=se.mkv.itintegration.getuserprofile.v2"
    ]

    wsdlsToGenerate = [
            commonOptions + [
                    "-wsdlLocation",
                    "classpath:interactions/GetUserProfile/GetUserProfileInteraction_2.0_RIVTABP21.wsdl",
                    "$wsdlDir/interactions/GetUserProfile/GetUserProfileInteraction_2.0_RIVTABP21.wsdl"
            ]
    ]
}