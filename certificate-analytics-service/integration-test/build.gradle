dependencies {
    implementation project(':app')

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.awaitility:awaitility'
    testImplementation 'org.springframework.boot:spring-boot-starter-activemq'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.testcontainers:mysql'
    testImplementation 'org.testcontainers:activemq'

    testImplementation testFixtures(project(":app"))
}

test {
    enabled = false
}

testing {
    suites {
        integrationTest(JvmTestSuite) {
            useJUnitJupiter()

            sources {
                java {
                    srcDirs = ['src/test/java']
                }
                resources {
                    srcDirs = ['src/test/resources']
                }
            }
            configurations {
                integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor
                integrationTestImplementation.extendsFrom testImplementation
                integrationTestCompileOnly.extendsFrom testCompileOnly
            }
            targets.all {
                testTask.configure {
                    shouldRunAfter(test)
                }
            }
        }
    }
}
