plugins {
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'org.sonarqube' version '4.4.1.3373'
    id "org.owasp.dependencycheck" version "7.4.4"
}

ext {
    springbootMavenBomVersion = '2.7.6'
    greenMailVersion = "1.6.11"
}

group = 'se.inera.intyg.css'
version = '0.0.1-SNAPSHOT'

allprojects {
    repositories {
        mavenCentral()
    }
}

sonar {
    properties {
        property "sonar.projectKey", "intyg-cts"
        property "sonar.projectName", "intyg-cts"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'org.sonarqube'
    apply plugin: 'org.owasp.dependencycheck'

    sourceCompatibility = '17'
    targetCompatibility = '17'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}"
        }
    }

    jacocoTestReport {
        dependsOn test

        reports {
            xml.required = true
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        "**/testability/*"
                ])
            }))
        }
    }

    dependencyCheck {
        formats = List.of("HTML", "JSON")
    }

    sonar {
        properties {
            property "sonar.exclusions", "**/testability/*"
            property "sonar.dependencyCheck.htmlReportPath", "${buildDir}/reports/dependency-check-report.html"
        }
    }
}