apply plugin: "java-test-fixtures"

dependencies {
    implementation project(':domain')
    implementation project(':app')
    implementation project(':integration-intyg-proxy-service')

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    testCompileOnly 'com.fasterxml.jackson.core:jackson-annotations'
    testCompileOnly 'com.fasterxml.jackson.core:jackson-databind'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-test'
    testImplementation "org.testcontainers:mysql:${testContainersMysqlVersion}"
    testImplementation "org.testcontainers:activemq:${testContainersActivemqVersion}"
    testImplementation 'org.testcontainers:mockserver'
    testImplementation "org.mock-server:mockserver-client-java:${mockServerClientVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-activemq'
    testImplementation 'org.awaitility:awaitility'
    testImplementation testFixtures(project(":app"))
    testImplementation testFixtures(project(":domain"))
}

testing {
    suites {
        integrationTest(JvmTestSuite) {
            testType = TestSuiteType.INTEGRATION_TEST
            description = 'Runs integration tests.'
            group = 'verification'

            sources {
                java {
                    srcDirs = ['src/test/java']
                }
                resources {
                    srcDirs = ['src/test/resources']
                }
            }
            configurations {
                integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor
                integrationTestImplementation.extendsFrom testImplementation
                integrationTestCompileOnly.extendsFrom testCompileOnly
            }
            targets.all {
                testTask.configure {
                    shouldRunAfter(test)
                    minHeapSize = "512m"
                    maxHeapSize = "3g"
                }
            }
        }
    }
}